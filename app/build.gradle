apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'realm-android'

android {
    compileSdkVersion 30

    defaultConfig {
        def getGitVersion = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--long'
                standardOutput = stdout
            }
            def gitDescribe = stdout.toString().trim()
            return gitDescribe.substring(0, gitDescribe.lastIndexOf("-"))
        }
        def getVersionCode = { ->
            def gitVersion = getGitVersion().replace("-", ".")
            String[] version = gitVersion.split('\\.')
            int versionCode = Integer.parseInt(version[0]) * 1000000 + Integer.parseInt(version[1]) * 10000 + Integer.parseInt(version[2])
            return versionCode
        }
        def getVersionName = {
            def gitVersion = getGitVersion()
            return gitVersion.replace("-", ".")
        }

        buildConfigField "String", "APP_CENTER_KEY", "\""+getUsername()+"\""

        applicationId "com.socketmobile.stockcount"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.romandanylyk:pageindicatorview:1.0.1'
    implementation 'com.socketmobile:capture-android:1.6.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    def appCenterSdkVersion = '3.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    testImplementation 'junit:junit:4.12'
}

def getUsername() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("app_center_key");
}
